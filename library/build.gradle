apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

afterEvaluate {
  apply from: 'gradle/publish.gradle'
}

/**
 * Holds the information associated with the library needed for the
 * bintray plugin to publish the artifact
 */
class LibraryInfo {
  static Integer versionMajor = 2
  static Integer versionMinor = 0
  static Integer versionPatch = 1

  static String artifactId = 'playlistcore'
  static String groupId = 'com.devbrackets.android'
  static String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
  static Integer versionCode = versionMajor * 10_000 + versionMinor * 1_000 + versionPatch * 100
}

static def getLibraryInfo() {
  return new LibraryInfo()
}

dependencies {
  // Kotlin
  api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

  // Android
  api 'androidx.media:media:1.4.3'
}

android {
  compileSdkVersion 31

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 31
  }

  lintOptions {
    abortOnError false
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

// Disables javadoc because it fails on kotlin files
tasks.withType(Javadoc).all {
  enabled = false
}